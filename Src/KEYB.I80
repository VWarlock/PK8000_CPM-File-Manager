;------------------------------------------------------------------------
; Запрос статуса консоли.
;------------------------------------------------------------------------
; на выходе:
;       ZF      - 1: буфер пуст
;                 0: есть символ[ы]
; usage: A, HL, DE
kbIsPressed:
        lhld    0FA2Ch          ; адрес чтения из буфера клавиатуры
        xchg
        lhld    0FA2Ah          ; адрес записи в буфере клавиатуры
        jmp     ucomp


;------------------------------------------------------------------------
; возвращает код нажатой клавиши (с ожиданием ввода)
;------------------------------------------------------------------------
; save: BC, DE, HL
kbGetKey:
        push    H
        push    D
        push    B
    @kbwait:
        call    kbIsPressed
        jz      @kbwait
        ; извлекаем из буфера
        lhld    0FA2Ch          ; адрес чтения из буфера клавиатуры
        mov     B, M
        inx     H
        lxi     D, 0FC87h
        call    ucomp      ; достигли конца буфера ввода?
        jnz     @kbgksv
        lxi     H, 0FB85h  ; то переходим в его начало
    @kbgksv:
        shld    0FA2Ch
        mov     A, B
        pop     B
        pop     D
        pop     H
        ret

;------------------------------------------------------------------------
; очищает очередь клавиатуры
;------------------------------------------------------------------------
kbFlush:
        push    H
        lxi     H, 0FB85h
        shld    0FA2Ch
        shld    0FA2Ah
        pop     H
        ret


;------------------------------------------------------------------------
; опрос функциональных клавиш
;------------------------------------------------------------------------
; на выходе:
;       A       - 0     : ничего не нажато
;                 1..5  : нажата [F1..F5]
;                 6..10 : нажата Shift+[F1..F5]
;                 11..15: нажата Алф+[F1..F5]
; used: A, DE
kbGetFuncKeys:
        call    kbGetFXStatus
        mov     A, D
        ora     A
        rz                      ; -> ничего не нажато, уходим
        lda     0FD85h          ; A - флаги: |O|АЛФ|ГРФ|УПР|РГ
        mov     E, A
        ani     8               ; нажат АЛФ?
        jz      @isShiftFx      ; -> нет
        mvi     A, 0Ah
        add     D               ; return (ALF+Fx)
        ret
    @isShiftFx:
        mov     A, E
        ani     1               ; нажат РГ (shift)?
        mov     A, D
        rz
        adi     5
        ret




;------------------------------------------------------------------------
; возвращает статус функциональных клавиш
;------------------------------------------------------------------------
; на выходе:
;       D       - 0..5 (0 - ничего не нажато, 1 - F1, 2 - F2 ... 5 - F5)
kbGetFXStatus:
        call    KeybCol7
        mvi     D, 5
        cpi     2               ; нажата F5?
        rz                      ; -> return 5
        dcr     D
        cpi     1               ; нажата F4?
        rz                      ; -> return 4
        dcr     D
        call    KeybCol6
        ani     0E0h            ; статус F3|F2|F1
        cpi     80h             ; нажата F3?
        rz                      ; -> return 3
        dcr     D
        cpi     40h             ; нажата F2?
        rz                      ; -> return 2
        dcr     D
        cpi     20h             ; нажата F1?
        rz                      ; -> return 1
        dcr     D
        ret                     ; return 0



;------------------------------------------------------------------------
; читает строку матрицы клавиатуры
;------------------------------------------------------------------------
; на выходе:
;       A       - флаги: XX|XX|XX|СТОП|TAB|ПРФ|F5|F4
KeybCol7:
        mvi     A, 7
        jmp     KeybColumn
;------------------------------------------------------------------------
; читает строку матрицы клавиатуры
;------------------------------------------------------------------------
; на выходе:
;       A       - флаги: F3|F2|F1|O|АЛФ|ГРФ|УПР|РГ
KeybCol6:
        mvi     A, 6
;------------------------------------------------------------------------
; читает одну строку матрицы клавиатуры
;------------------------------------------------------------------------
; на входе:
;     A - номер строки матрицы [0..9]
; на выходе:
;     A - биты состояния 8 кнопок строки
KeybColumn:
        di                      ; выключаем прерывания
        push    B
        ani     0Fh             ; по идее лучше заменить на cpi 9/jbe
        mov     B, A
        in      82h
        ani     0F0h
        ora     B               ; номер строки матрицы клавиатуры
        out     82h
        in      81h
        pop     B
        ei
        cma
        ret



